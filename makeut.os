#Использовать v8runner
#Использовать asserts
#Использовать fs
#Использовать tempfiles

Перем юТест;
Перем УправлениеКонфигуратором;
Перем ВременныеФайлы;
Перем Лог;

//Локальные параметры
Перем мКаталогСборки;
Перем мНоваяКонфигурацияЕРП;
Перем мФайлНастроекВырезки;
Перем мФайлНастроекОбновления;
Перем мФайлОбработкиВырезки;
Перем мФайлЛога;
Перем мФайлЛога1С;


Процедура Инициализация()
	
	УправлениеКонфигуратором = Новый УправлениеКонфигуратором;
	УправлениеКонфигуратором.УстановитьИмяФайлаСообщенийПлатформы(мФайлЛога1С,Ложь);
	УправлениеКонфигуратором.ИспользоватьВерсиюПлатформы("8.3.12");
	УправлениеКонфигуратором.КаталогСборки(мКаталогСборки);

	Лог = Логирование.ПолучитьЛог("karuk.conf.makeut");
	Лог.УстановитьУровень(УровниЛога.Отладка);
	Лог.УстановитьРаскладку(ЭтотОбъект);


	ФайлЖурнала = Новый ВыводЛогаВФайл;
	ФайлЖурнала.ОткрытьФайл(мФайлЛога);
	Консоль = Новый ВыводЛогаВКонсоль;

	Лог.ДобавитьСпособВывода(ФайлЖурнала);
	Лог.ДобавитьСпособВывода(Консоль);
	Лог.Отладка("Инициализация выполнена");

	ЛогКоманда = Логирование.ПолучитьЛог("oscript.lib.commands");
	ЛогКоманда.ДобавитьСпособВывода(Консоль);


КонецПроцедуры

Процедура ВырезатьТегСправки(КаталогФайлов,ИмяТега = "ERP")
	
	НайденныеФайлы = НайтиФайлы(КаталогФайлов, "*.Help.ru.html");
	
	Для Каждого Файл Из НайденныеФайлы Цикл
		
		ФайлИзменен = Ложь;
		
		Текст = Новый ТекстовыйДокумент;
		Текст.Прочитать(Файл.ПолноеИмя);
		ПолныйТекст = Текст.ПолучитьТекст(); 

		РегулярноеВыражение = Новый РегулярноеВыражение( "<ERP>((.*?)|\n)+<\/ERP>" );
	
		ТекстБезКомментариев = РегулярноеВыражение.Заменить(ПолныйТекст, "" );
		
		Если ПолныйТекст <> ТекстБезКомментариев Тогда
			Текст.УстановитьТекст(ТекстБезКомментариев);
			Текст.Записать(Файл.ПолноеИмя);
	    КонецЕсли; 
		
	КонецЦикла;
	

КонецПроцедуры


Функция Форматировать(Знач Уровень, Знач Сообщение) Экспорт

	Возврат СтрШаблон("%1: %2 - %3", ТекущаяДата(), УровниЛога.НаименованиеУровня(Уровень), Сообщение);

КонецФункции

Функция ОбернутьВКавычки(Знач Строка);
	Если Лев(Строка, 1) = """" и Прав(Строка, 1) = """" Тогда
		Возврат Строка;
	Иначе
		Возврат """" + Строка + """";
	КонецЕсли;
КонецФункции

Функция ПолучитьПоследнийФайл(КаталогПоискаФайлов)

	МассивФайлов = НайтиФайлы(КаталогПоискаФайлов,"ERP_2018*_part.cf",Ложь);
	
	Если МассивФайлов.КОличество() = 0 Тогда
		ВызватьИсключение "Не найдены файлы для обновления в каталоге "+КаталогПоискаФайлов;
	КонецЕсли;
	
	ПоследнийФайл = МассивФайлов[0];
	Для каждого ФайлЕРП Из МассивФайлов Цикл
		Если ФайлЕРП.Имя > ПоследнийФайл.Имя Тогда
			ПоследнийФайл = ФайлЕРП;
		КонецЕсли;
	КонецЦикла;

	Возврат ПоследнийФайл;

КонецФункции	

мРабочийКаталог = "E:\localization"; 
мКаталогОсновнойБазы = "E:\bases\base\Титан32"; //База с УТ32, подключенная к хранилищу
мПутьКХранилищу = "tcp://Orion:3542/Титан32";

мФайлНастроекВырезки =  ОбъединитьПути(мРабочийКаталог,"src\TRADE_AutoMoveMergeSettings.xml");
мФайлНастроекОбновления = ОбъединитьПути(мРабочийКаталог,"src\TRADE_AutoMoveUpdateSettings.xml");
мФайлОбработкиВырезки = ОбъединитьПути(мРабочийКаталог,"src\DeleteNoTrade.epf");
мФайлВыгрузкиШаблона = ОбъединитьПути(мРабочийКаталог,"src\titan32.1CD");

мКаталогСборки =  ОбъединитьПути(мРабочийКаталог,"Титан32\build");
мФайлПоставки = ОбъединитьПути(мКаталогСборки,"postavka.cf");
мКаталогВыгрузки = ОбъединитьПути(мКаталогСборки,"out_files");

мФайлПоследнейКонфигурации = ПолучитьПоследнийФайл("\\orion\localization");
мНоваяКонфигурацияЕРП = мФайлПоследнейКонфигурации.ПолноеИмя;

мСуффиксЛога = СтрЗаменить(мФайлПоследнейКонфигурации.ИмяБезРасширения,"_part","");
мФайлЛога = ОбъединитьПути(мРабочийКаталог,мСуффиксЛога+".log");
мФайлЛога1С = ОбъединитьПути(мРабочийКаталог,мСуффиксЛога+"_1C.log");


Если ФС.ФайлСуществует(мФайлЛога) Тогда
	Сообщить("Файл "+мНоваяКонфигурацияЕРП+ " уже загружался!
	         | Для повторной загрузки удалите файл лога " + мФайлЛога);
        ЗавершитьРаботу(2);
КонецЕсли; 

Инициализация();


Лог.Отладка("Обновление по файлу "+мНоваяКонфигурацияЕРП);

НачатьСЭтапа = 1;

Если НачатьСЭтапа <= 1 Тогда
	

//1. ВЫрезка из ЕРП объектов для УТ
Лог.Отладка("Запуск загрузки");

ФС.ОбеспечитьПустойКаталог(мКаталогСборки);
ФС.ОбеспечитьПустойКаталог(УправлениеКонфигуратором.ПутьКВременнойБазе());
КопироватьФайл(мФайлВыгрузкиШаблона,УправлениеКонфигуратором.ПутьКВременнойБазе()+"\1Cv8.1CD");
//УправлениеКонфигуратором.СоздатьФайловуюБазу(УправлениеКонфигуратором.ПутьКВременнойБазе(),мФайлВыгрузкиШаблона);

Лог.Отладка("Запуск ОбъединитьКонфигурациюСФайлом");
УправлениеКонфигуратором.ОбъединитьКонфигурациюСФайлом(мНоваяКонфигурацияЕРП,мФайлНастроекВырезки,Неопределено,Ложь,Истина);

КонецЕсли;


Если НачатьСЭтапа <= 2 Тогда

//2. Вырезка из модулей фрагментов НЕ УТ
ФС.ОбеспечитьПустойКаталог(мКаталогВыгрузки);

Лог.Отладка("Запуск ВыгрузитьКонфигурациюВФайлы");
УправлениеКонфигуратором.ВыгрузитьКонфигурациюВФайлы(мКаталогВыгрузки,"Plain");

Лог.Отладка("Запуск ЗапуститьВРежимеПредприятия");
ДополнительныеПараметры = " /Execute "+ ОбернутьВКавычки(мФайлОбработкиВырезки)+ " /CUpdateTextFiles;FilesCatalog="+ ОбернутьВКавычки(мКаталогВыгрузки);
Лог.Отладка(ДополнительныеПараметры);
УправлениеКонфигуратором.ЗапуститьВРежимеПредприятия(,,ДополнительныеПараметры);
ВырезатьТегСправки(мКаталогВыгрузки);
Лог.Отладка("Запуск ЗагрузитьКонфигурациюИзФайлов");
УправлениеКонфигуратором.ЗагрузитьКонфигурациюИзФайлов(мКаталогВыгрузки);


Лог.Отладка("Запуск ОбновитьКонфигурациюБазыДанных");
УправлениеКонфигуратором.ОбновитьКонфигурациюБазыДанных();

КонецЕсли;

Если НачатьСЭтапа <= 3 Тогда

//3. Создаем файл поставки с данными из ЕРП
Лог.Отладка("Запуск СоздатьФайлыПоставки()");
УправлениеКонфигуратором.СоздатьФайлыПоставки(мФайлПоставки);

КонецЕсли;


//4. Обновляем УТ по ЕРП
Лог.Отладка("Обновление конфигурации");
//УправлениеКонфигуратором.УстановитьКонтекст("/F""" + мКаталогОсновнойБазы + """","Федоров (администратор)","");
УправлениеКонфигуратором.УстановитьКонтекст("/F""" + мКаталогОсновнойБазы + """","Администратор (ОрловАВ)","");

//Захватить в хранилище
Лог.Отладка("Запуск ЗахватитьОбъектыВХранилище");
УправлениеКонфигуратором.ЗахватитьОбъектыВХранилище(мПутьКХранилищу,"auto","","",Истина);

Лог.Отладка("Запуск ОбновитьКонфигурациюИзФайла");
УправлениеКонфигуратором.ОбновитьКонфигурациюИзФайла(мФайлПоставки,мФайлНастроекОбновления,Ложь,Истина,Истина);

Лог.Отладка("Запуск ПоместитьИзмененияОбъектовВХранилище");
УправлениеКонфигуратором.ПоместитьИзмененияОбъектовВХранилище(мПутьКХранилищу,"auto","",,"Автообновление по "+мНоваяКонфигурацияЕРП,Ложь,Истина);

Лог.Отладка("Запуск ОбновитьКонфигурациюБазыДанных");
УправлениеКонфигуратором.ОбновитьКонфигурациюБазыДанных();


ИсключениеПриОшибке = УправлениеКонфигуратором.ИсключениеПриОшибкеВыполненияКоманды(Ложь); //Тут всегда ошибки, это нормально
УправлениеКонфигуратором.ВыполнитьСинтаксическийКонтроль(Истина,Ложь,Истина,Ложь,Ложь);
УправлениеКонфигуратором.ИсключениеПриОшибкеВыполненияКоманды(ИсключениеПриОшибке);

Лог.Отладка("Обновление успешно завершено");

